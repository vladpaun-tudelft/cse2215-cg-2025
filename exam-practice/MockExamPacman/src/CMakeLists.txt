
if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/provided.cpp")
    set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_BACKUP ${CMAKE_ARCHIVE_OUTPUT_DIRECTORY})
    if (WIN32)
        set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/prebuilt/win_x64")
    elseif (APPLE)
        set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/prebuilt/macos_arm64")
    else()
        set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/prebuilt/linux_x64")
    endif()
    add_library(ProvidedLib STATIC provided.cpp)
    target_link_libraries(ProvidedLib PUBLIC CGFramework)
    target_compile_features(ProvidedLib PUBLIC cxx_std_20)
    set_project_warnings(ProvidedLib)
    enable_sanitizers(ProvidedLib)
    set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_ARCHIVE_OUTPUT_DIRECTORY_BACKUP})
else()
    add_library(ProvidedLib STATIC IMPORTED)
    if (WIN32)
        set_target_properties(ProvidedLib PROPERTIES IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/prebuilt/win_x64/ProvidedLib.lib")
    elseif (APPLE)
        set_target_properties(ProvidedLib PROPERTIES IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/prebuilt/macos_arm64/libProvidedLib.a")
    else()
        set_target_properties(ProvidedLib PROPERTIES IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/prebuilt/linux_x64/libProvidedLib.a")
    endif()
endif()

add_library(BachelorExamPacmanLib STATIC "your_code.cpp")
target_link_libraries(BachelorExamPacmanLib PUBLIC CGFramework ProvidedLib)
target_compile_features(BachelorExamPacmanLib PUBLIC cxx_std_20)
set_project_warnings(BachelorExamPacmanLib)
enable_sanitizers(BachelorExamPacmanLib)

add_executable(BachelorExamPacman "main.cpp" "draw.cpp")
target_link_libraries(BachelorExamPacman PRIVATE BachelorExamPacmanLib CGFramework OpenGL::GLU)
target_compile_features(BachelorExamPacman PRIVATE cxx_std_20)
enable_sanitizers(BachelorExamPacman)
set_project_warnings(BachelorExamPacman)

target_compile_definitions(BachelorExamPacman PRIVATE "-DGL_SILENCE_DEPRECATION=1")
target_compile_definitions(BachelorExamPacman PRIVATE "-DDATA_DIR=\"${CMAKE_CURRENT_LIST_DIR}/data/\"" "-DOUTPUT_DIR=\"${CMAKE_CURRENT_LIST_DIR}/\"")

